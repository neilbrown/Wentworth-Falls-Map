#!bin/nash

osm=$1

{
sed '/^<.osm>$/d' $osm
gawk '
	function abbrev_way(name)
	{
		sub(/Avenue$/,"Ave", name)
		sub(/Circle$/,"Cir", name)
		sub(/Close$/, "Cl", name)
		sub(/Crescent$/, "Cr", name)
		sub(/Drive$/, "Dr", name)
		sub(/Grove$/, "Gr", name)
		sub(/Highway$/, "Hwy", name)
		sub(/Lane$/, "Ln", name)
		sub(/Parade$/, "Pde", name)
		sub(/Place$/, "Pl", name)
		sub(/Road$/, "Rd", name)
		sub(/Street$/, "St", name)
		return name
	}
	BEGIN {
		prio["trunk"] = 1
		prio["secondary"] = 3
		prio["tertiary"] = 4
		prio["residential"] = 5
		prio["service"] = 6
		prio["track"] = 7
		prio["unclassified"] = 8
	}

	$1 == "<node" && $3 ~ /lat=/ {
		split($0, ll, /"/);
		lon[ll[2]] = ll[6];
		lat[ll[2]] = ll[4];
	}
	$1 == "<way" {inway = 1; delete(refs); hwy="";
		split($2, ids, /"/); id=ids[2];
		}
	$1 == "</way>" {
		n = name
		h = hwy
		fr = firstref
		lr = lastref

		inway = 0;
		name = "";
		hwy = "";
		firstref = ""

		if (!n || ! h || !fr)
			#nothing here...
			next;

		if (! (n in alias))
			alias[n] = abbrev_way(n)

		# See if we can add an existing way
		for (id2 in ways[n]) {
			delete r
			for (i in idrefs[id2])
				r[i] = idrefs[id2][i]
			if (fr == r[0]) {
				delete(rn);
				for (i = length(refs); i > 0; i--) {
					rn[length(rn)] = refs[i-1];
				}
				for (i = 1 ; i < length(r); i++) {
					rn[length(rn)] = r[i];
				}
				delete refs
				for (i in rn) refs[i] = rn[i]
				delete idrefs[id2];
				delete ways[n][id2]
				fr = refs[0]
				lr = refs[length(refs)-1]
			} else if (fr == r[length(r)-1]) {
				for (i = 1 ; i < length(refs); i++) {
					r[length(r)] = refs[i];
				}
				delete refs
				for (i in r) refs[i] = r[i]
				delete idrefs[id2]
				delete ways[n][id2]
				fr = refs[0]
				lr = refs[length(refs)-1]
			} else if (lr == r[0]) {
				for (i = 1 ; i < length(r); i++) {
					refs[length(refs)] = r[i];
				}
				delete idrefs[id2]
				delete ways[n][id2]
				fr = refs[0]
				lr = refs[length(refs)-1]
			} else if (lr == r[length(r)-1]) {
				for (i = length(r)-1 ; i > 0; i--) {
					refs[length(refs)] = r[i-1];
				}
				delete idrefs[id2]
				delete ways[n][id2]
				fr = refs[0]
				lastref = refs[length(refs)-1]
			}
		}

		# refs has been extended if possible
		for (i in refs)
			idrefs[id][i] = refs[i];

		ways[n][id] = 1;


		if (h in prio && n in hwyof && prio[h] > prio[hwyof[n]]) h = hwyof[n];
		if (prio[h] > 5) h = "residential"
		hwyof[n] = h
	}
			
	inway { split($0, wd, /"/) }
	inway && wd[2] == "name" { name = wd[4] }
	inway && wd[2] == "highway" { hwy = wd[4] }

	inway && wd[1] ~ "<nd ref=" { refs[length(refs)] = wd[2]; lastref=wd[2]; if (!firstref) firstref=wd[2]; }

	END {
		for (n in ways) {
			for (id in ways[n]) {
				printf "<way id=\"A%s\">\n", id;
				for (i in idrefs[id]) {
					printf "  <nd ref=\"%s\"/>\n", idrefs[id][i]
				}
				printf "  <tag k=\"highway\" v=\"%s_name\"/>\n", hwyof[n];

				printf "  <tag k=\"name\" v=\"%s\"/>\n", alias[n]
				printf "</way>\n"
			}
		}
	}
' $osm
echo "</osm>"
}
